GetFilename(fixedVolumeHead ${fixedVolume} NAME_WITHOUT_EXTENSION)
GetFilename(fixedVolumeTail ${fixedVolume} NAME)
GetFilename(OutputDir ${fixedVolume} PATH)
If (${OutputDir} == '')
  set (OutputDir '.')
EndIf(${OutputDir})

GetFilename(movingVolumeHead ${movingVolume} NAME_WITHOUT_EXTENSION)
GetFilename(movingVolumeTail ${movingVolume} NAME)

# FA map creation
echo()
echo('FA map creation...')
echo('fixed FA map creation...')
If (${outputFixedFAVolume} == '')
  set (fixedFAMap ${OutputDir}/${fixedVolumeHead}_FA.nrrd)
Else(${outputFixedFAVolume})
  set (fixedFAMap ${outputFixedFAVolume})
EndIf(${outputFixedFAVolume})
If (${fixedMaskVolume} != '')
  set (commanddtiprocess ${dtiprocessCmd} --dti_image ${fixedVolume} -f ${fixedFAMap} --mask ${fixedMaskVolume})
Else(${fixedMaskVolume})
 set (commanddtiprocess ${dtiprocessCmd} --dti_image ${fixedVolume} -f ${fixedFAMap}) 
EndIf (${fixedMaskVolume})
Run (outputdtiprocess ${commanddtiprocess} errordtiprocess)
If(${errordtiprocess} != '')
  echo('Error dtiprocess: ' ${errordtiprocess})
  exit()
Endif(${errordtiprocess})

echo('moving FA map creation...')
If (${outputMovingFAVolume} == '')
  set (movingFAMap ${OutputDir}/${movingVolumeHead}_FA.nrrd)
Else(${outputMovingFAVolume})
  set (movingFAMap ${outputMovingFAVolume})
EndIf(${outputMovingFAVolume})
If (${movingMaskVolume} != '')
  set (commanddtiprocess ${dtiprocessCmd} --dti_image ${movingVolume} -f ${movingFAMap} --mask ${movingMaskVolume})
Else(${movingMaskVolume})
  set (commanddtiprocess ${dtiprocessCmd} --dti_image ${movingVolume} -f ${movingFAMap})
EndIf(${movingMaskVolume})
Run (outputdtiprocess ${commanddtiprocess} errordtiprocess)
If(${errordtiprocess} != '')
  echo('Error dtiprocess: ' ${errordtiprocess})
  exit()
Endif(${errordtiprocess})


#Registration
If (${BRAINSRegistrationType} == 'Rigid' || ${BRAINSRegistrationType} == 'Affine' || ${BRAINSRegistrationType} == 'BSpline')
  set(IsDemonsWarping 0)
Else (${BRAINSRegistrationType})
  set(IsDemonsWarping 1)
EndIf(${BRAINSRegistrationType})

# Registration via BRAINSFit 
set (Transform '')
If (${BRAINSRegistrationType} == 'Rigid')
  set (TransformType 'Rigid')
  set (RegSuffix 'RReg')
Else(${BRAINSRegistrationType})
  If (${BRAINSRegistrationType} == 'BSpline')
    set (TransformType 'Rigid,Affine,BSpline')
    set (RegSuffix 'BSpline')
  Else()
     set (TransformType 'Rigid,Affine')
     set (RegSuffix 'AffReg')
  EndIf()
EndIf(${BRAINSRegistrationType})
echo()
echo(${TransformType} ' registration...')

If (${outputResampledFAVolume} != '')
  set (ResampledFAMap ${outputResampledFAVolume})
Else(${outputResampledFAVolume})
  set (ResampledFAMap ${OutputDir}/${movingVolumeHead}_FA_${RegSuffix}.nrrd)
Endif(${outputResampledFAVolume})
If (${outputTransform} != '')
  set (Transform ${outputTransform})
Else(${outputTransform})
  set (Transform ${OutputDir}/${movingVolumeHead}_FA_${RegSuffix}.txt)
Endif(${outputTransform})
If (${initialTransform} != '')
  set (commandBRAINSFit ${BRAINSFitCmd} --fixedVolume ${fixedFAMap} --movingVolume ${movingFAMap} --initialTransform ${initialTransform} --outputTransform ${Transform} --outputVolume ${ResampledFAMap} --outputVolumePixelType ushort --transformType ${TransformType} --interpolationMode Linear)
Else(${initialTransform})
  set (commandBRAINSFit ${BRAINSFitCmd} --fixedVolume ${fixedFAMap} --movingVolume ${movingFAMap} --initializeTransformMode ${initializeTransformMode} --outputTransform ${Transform} --outputVolume ${ResampledFAMap} --outputVolumePixelType ushort --transformType ${TransformType} --interpolationMode Linear)
Endif(${initialTransform})
Run (outputBRAINSFit ${commandBRAINSFit} errorBRAINSFit)
#If(${errorBRAINSFit} != '')
#  echo('Error BRAINSFit: ' ${errorBRAINSFit})
#  exit()
#Endif(${errorBRAINSFit})
If (${IsDemonsWarping} == 1)
  DeleteFile(${ResampledFAMap})
EndIf(${IsDemonsWarping})

# Warping
If (${IsDemonsWarping} == 1)
  echo()
  echo('Warping...')
  If (${outputResampledFAVolume} != '')
    set (ResampledFAMap ${outputResampledFAVolume})
  Else(${outputResampledFAVolume})
    set (ResampledFAMap ${OutputDir}/${movingVolumeHead}_FA_warp.nrrd)
  EndIf(${outputResampledFAVolume})
  If (${outputDeformationFieldVolume} != '')
    set (DeformationField ${outputDeformationFieldVolume})
  Else(${outputDeformationFieldVolume})
    set (DeformationField ${OutputDir}/${movingVolumeHead}_FA_warpfield.nrrd)
  EndIf(${outputDeformationFieldVolume})
  set (commandBRAINSDemonWarp ${BRAINSDemonWarpCmd} --fixedVolume ${fixedFAMap} --movingVolume ${movingFAMap} --outputVolume ${ResampledFAMap} --outputDeformationFieldVolume ${DeformationField} --outputPixelType ushort --interpolationMode Linear --registrationFilterType ${BRAINSRegistrationType} --histogramMatch --numberOfHistogramBins ${numberOfHistogramLevels} --numberOfMatchPoints ${numberOfMatchPoints} --initializeWithTransform ${Transform} --smoothDeformationFieldSigma ${BRAINSsmoothDefFieldSigma} --numberOfPyramidLevels ${BRAINSnumberOfPyramidLevels} --arrayOfPyramidLevelIterations ${BRAINSarrayOfPyramidLevelIterations})
  If (${initialDeformationField} != '')
    set (commandBRAINSDemonWarp ${commandBRAINSDemonWarp} --initializeWithDeformationField ${initialDeformationField})
  EndIf(${initialDeformationField})
  Run (outputBRAINSDemonWarp ${commandBRAINSDemonWarp} errorBRAINSDemonWarp)
  If(${errorBRAINSDemonWarp} != '')
    echo('Error BRAINSDemonWarp: ' ${errorBRAINSDemonWarp})
    exit()
  Endif(${errorBRAINSDemonWarp})
Endif(${IsDemonsWarping})

# DTI resampling
echo()
echo('DTI resampling...')
If (${outputVolume} != '')
  set (ResampledDTI ${outputVolume})
Else(${outputVolume})
  set (ResampledDTI ${OutputDir}/${movingVolumeHead}_warp.nrrd)
EndIf(${outputVolume})
set (commandResampleDTI ${ResampleDTICmd} ${movingVolume} ${ResampledDTI} --Reference ${fixedVolume} --transformationFile ${Transform})
If (${IsDemonsWarping} == 1)
  set (commandResampleDTI ${commandResampleDTI} --defField ${DeformationField} --hfieldtype displacement)
EndIf(${IsDemonsWarping})
Run (outputResampleDTI ${commandResampleDTI} errorResampleDTI)
If(${errorResampleDTI} != '')
  echo('Error ResampleDTI: ' ${errorResampleDTI})
  exit()
Endif(${errorResampleDTI})

#Delete temporary files
#If (${outputVolume} == '')
#  DeleteFile(${ResampledDTI})
#EndIf(${outputVolume})
If (${outputTransform} == '')
  DeleteFile(${Transform})
EndIf(${outputTransform})
#If (${outputDeformationFieldVolume} == '')
#  DeleteFile(${DeformationField})
#EndIf(${outputDeformationFieldVolume})
If (${outputFixedFAVolume} == '')
  DeleteFile(${fixedFAMap})
EndIf(${outputFixedFAVolume})
If (${outputMovingFAVolume} == '')
  DeleteFile(${movingFAMap})
EndIf(${outputMovingFAVolume})
If (${outputResampledFAVolume} == '')
  DeleteFile(${ResampledFAMap})
EndIf(${outputResampledFAVolume})
